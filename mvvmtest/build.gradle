def prouardEnable = false
def WPOS_APP_VERSION_CODE = "${new Date().format('yyMMddHHmm')}".toInteger()
def WPOS_APP_VERSION_NAME = "HED.${WPOS_APP_VERSION_CODE}"

// sign
def WPOS_APP_STORE_FILE = "wall.keystore"
def WPOS_APP_STORE_PWD = "shellinfoem"
def WPOS_APP_KEY_ALIAS = "shellinfo"
def WPOS_APP_KEY_PWD = "shellinfoem"
apply plugin: 'com.android.application'
android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "org.hed.archdemo"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode WPOS_APP_VERSION_CODE
        versionName "${WPOS_APP_VERSION_NAME}"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    signingConfigs {
        wposConfig {
            storeFile file(WPOS_APP_STORE_FILE)
            storePassword WPOS_APP_STORE_PWD
            keyAlias WPOS_APP_KEY_ALIAS
            keyPassword WPOS_APP_KEY_PWD
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.wposConfig
            minifyEnabled prouardEnable
            useProguard prouardEnable
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }
    productFlavors {
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

}

//解决执行mockableAndroidJar失败的问题
gradle.taskGraph.whenReady {
    tasks.each { task ->
        if (task.name.equals('mockableAndroidJar')) {
            task.enabled = false
        }
    }
}

ext {
    arch_version = '1.0.0-beta2'
    support_library_version = '26.0.0-alpha1'
    dagger_version = "2.8"
    espresso_version = '2.2.2'
    runner_version = '0.5'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$support_library_version"
    compile "com.android.support:recyclerview-v7:$support_library_version"
    compile "com.android.support:design:$support_library_version"

    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'

    // Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.google.code.gson:gson:2.2.4'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'// retrofit+gson
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.3.0' // Rxjava与Retrofit结合使用必须添加这个依赖

    compile 'com.android.support.constraint:constraint-layout:1.1.0-beta3'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile "com.google.dagger:dagger:${dagger_version}"

    provided 'javax.annotation:jsr250-api:1.0'

    compile "android.arch.lifecycle:extensions:${arch_version}"
    compile "android.arch.persistence.room:runtime:${arch_version}"

    annotationProcessor "com.google.dagger:dagger-compiler:${dagger_version}"
    annotationProcessor "android.arch.lifecycle:compiler:${arch_version}"
    annotationProcessor "android.arch.persistence.room:compiler:${arch_version}"

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.8.9'
    testCompile("android.arch.core:core-testing:${arch_version}", {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })

    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile('org.threeten:threetenbp:1.3.2') {
        exclude module: "com.jakewharton.threetenabp:threetenabp:1.0.5"
    }
    androidTestAnnotationProcessor "com.google.dagger:dagger-compiler:${dagger_version}"
    androidTestCompile("android.arch.core:core-testing:${arch_version}", {
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-core-utils'
    })
    androidTestCompile 'org.mockito:mockito-android:2.7.19'

    androidTestCompile("com.android.support.test:runner:${runner_version}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test:rules:${runner_version}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-core:${espresso_version}") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}
